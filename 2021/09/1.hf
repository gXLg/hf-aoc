<= lib:io/readall

^1 <= ^0

^0 <=
^0 => 1 <= '\n'
^0 => 2 <= ^1
<= lib:str/split

#1 <= ^0 => 1
#2 <= #1
#3 <=
^1 <=

!1
  ^1 => #3 <= ^0 => (#3 + 3)
  #3 <= #3 + 1
  #1 <= #1 - 1
  #0 <= #0 + (1 - #1 / #1)
  #0 <= !1

:: amount of strings in #2

:: result here
#69 <=

:: copying first line and setting new line to all '9'
^2 <=
#1 <= -1
!2
  #1 <= #1 + 1
  #5 <= ^1 => 0 => #1
  ^2 => #1 <= '9'

  #0 <= #0 + (1 - #5 / #5)
  #0 <= !2
:: length of single line
#3 <= #1


:: cheating, lol
^1 => -1 <= ^2
^1 => #2 <= ^2
#5 <=
#1 <= #2
!3
  ^1 => #5 => -1 <= '9'
  ^1 => #5 => #3 <= '9'

  #5 <= #5 + 1
  #1 <= #1 - 1
  #0 <= #0 + (1 - #1 / #1)
  #0 <= !3

:: walking over the field
#5 <=
#1 <= #2
!4

  #6 <=
  #4 <= #3
  !5
    #7 <= ^1 => #5 => #6
    #8 <= ^1 => (#5 + 1) => #6
    #9 <= ^1 => (#5 - 1) => #6
    #10 <= ^1 => #5 => (#6 + 1)
    #11 <= ^1 => #5 => (#6 - 1)

    ^0 <=
    ^0 => 1 <= #7
    ^0 => 2 <= #8
    <= lib:math/geqnat
    #8 <= 1 - (^0 => 1)

    ^0 <=
    ^0 => 1 <= #7
    ^0 => 2 <= #9
    <= lib:math/geqnat
    #9 <= 1 - (^0 => 1)

    ^0 <=
    ^0 => 1 <= #7
    ^0 => 2 <= #10
    <= lib:math/geqnat
    #10 <= 1 - (^0 => 1)

    ^0 <=
    ^0 => 1 <= #7
    ^0 => 2 <= #11
    <= lib:math/geqnat
    #11 <= 1 - (^0 => 1)

    #69 <= #69 + (#8 * #9 * #10 * #11) * (#7 - '0' + 1)

    #6 <= #6 + 1
    #4 <= #4 - 1
    #0 <= #0 + (1 - #4 / #4)
    #0 <= !5

  #5 <= #5 + 1
  #1 <= #1 - 1
  #0 <= #0 + (1 - #1 / #1)
  #0 <= !4


^0 <=
^0 => 1 <= #69
<= lib:io/putnat
&1 <= '\n'
